group 'io.dolby.comms.sdk.flutter'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.7.10'
    ext.voxeet_sdk_version = "3.9.0-beta.1"
    ext.dagger_version = "2.40.5"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}
def localProperties = new Properties()

def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')

print(flutterRoot)

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'


android {
    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 21
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            buildConfigField "String", "SDK_VERSION", COMMS_SDK_VERSION
            buildConfigField "String", "COMPONENT_NAME", COMPONENT_NAME
        }

        debug {
            buildConfigField "String", "SDK_VERSION", COMMS_SDK_VERSION
            buildConfigField "String", "COMPONENT_NAME", COMPONENT_NAME
        }
    }
}

dependencies {
    // uncomment this to gradle sync for android plugin in android studio, comment again before build
//    implementation files("$flutterRoot/bin/cache/artifacts/engine/android-arm64-release/flutter.jar")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    def useMockSDK = project.properties.containsKey("useMockSDK") ? project.properties.get("useMockSDK") : false
    if (useMockSDK) {
        implementation project(":dolbyMock")
        implementation "io.dolby:promise:2.8.1"
        implementation "org.greenrobot:eventbus:3.1.1"
        implementation "io.dolby:utils:3.8.1"
    } else {
        implementation "io.dolby:sdk:$voxeet_sdk_version"
    }
    implementation project(path: ':flutter_plugin_android_lifecycle')

}
